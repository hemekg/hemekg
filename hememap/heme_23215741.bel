#######################
# Document Properties #
#######################

SET DOCUMENT Licenses    = "MIT License"
SET DOCUMENT ContactInfo = "farah.humayun@scai-extern.fraunhofer.de"

SET DOCUMENT Name        = "heme_23215741.bel"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Farah Humayun"
SET DOCUMENT Description = "Heme BEL model is a disease model based on Biological Expression Language (BEL) containing the core mechanisms of heme as modulator in Hemolytic diseases such as sickle-cell anemia, beta-thalassemia, malaria and autoimmune hemolytic anemia."

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

DEFINE NAMESPACE HM        AS PATTERN ".*"



###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN ".*"
DEFINE ANNOTATION TimePoint     AS PATTERN ".*"
DEFINE ANNOTATION Concentration     AS PATTERN ".*"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "Free Radic Res. 2013 Mar;47(3):164-71. doi: 10.3109/10715762.2012.756138. Epub 2013 Jan 9.", "23215741"} 

SET TextLocation = "Introduction"

SET Evidence = "While it is clear that PRDX2 plays an important 
role in protecting RBCs from oxidative stress, the relative 
importance of PRDX2 in scavenging H2O2 in RBC has not been 
fully elucidated."

p(HGNC:PRDX2) neg bp(MESH:"Oxidative Stress")

SET TextLocation = "Results"

SET Species = "Mus musculus"

SET Evidence = "These results indicate that heme 
degradation increases in RBCs of PRDX2 knockout 
mice in spite of the presence of catalase and GPx."

SET Cell = "erythrocyte"
p(MGI:Prdx2) neg deg(a(CHEBI:heme))

SET Evidence = "These results are consistent with 
our earlier observations that the presence of SOD1 
does not significantly inhibit or accelerate the 
formation of heme degradation products during in 
vitro autoxidation of oxyHb [18].."

p(MGI:Sod1) causesNoChange deg(a(CHEBI:heme))

SET Evidence = "The small increase in heme degradation 
in the absence of SOD1 may, however, be attributed to 
low levels of heme degradation products produced either 
by the increased levels of superoxide [17] or perhaps 
the peroxynitrite that forms due to the rapid reaction 
of superoxide with any NO present."

a(CHEBI:peroxynitrite) pos deg(a(CHEBI:heme))

SET Evidence = "However, metHb levels may increase when 
there is an increase in Hb autoxidation and the 
concomitant generation of H2O2 that is not 
scavenged by antioxidant defense enzymes making 
it more difficult for metHb-reductase to keep 
up with the metHb produced."

a(CHEBI:methemoglobin) pos a(CHEBI:"hydrogen peroxide")

SET Evidence = "As shown in Figure 2, the mean metHb levels 
were also significantly increased in PRDX2 knockout mice, 
but not in SOD1 knockout mice compared with control mice."

p(MGI:Prdx2) neg a(CHEBI:methemoglobin)

SET Evidence = "As shown in Figure 3, the level of 
heme degradation is highly correlated with the level 
of metHb in RBCs (R = 0.6233, p < 0.0177) supporting 
the hypothesis that the heme degradation product 
formed in PRDX2 knockout mice is associated with 
the un-scavenged H2O2 generated during Hb 
autoxidation."

SET Cell = "erythrocyte"
a(CHEBI:"hydrogen peroxide") pos deg(a(CHEBI:heme))

SET Evidence = "It has been shown that RBC oxidative 
stress that damages the membrane reduces the 
deformability and flexibility of cells."

bp(MESH:"Oxidative Stress") neg bp(MESH:"Erythrocyte Deformability")

SET Evidence = "Figure 4 shows a significant decrease in the 
elongation index, which is a measure of deformability, 
for the PRDX2 knockout mice."

p(MGI:Prdx2) pos bp(MESH:"Erythrocyte Deformability")

SET Evidence = "No significant change in the elongation 
index was found for the SOD1 knockout mice."

p(MGI:Sod1) causesNoChange bp(MESH:"Erythrocyte Deformability")

SET Evidence = "PRDX2 deficiency, thus, causes cells to undergo 
more oxidative stress during in vitro aging,
even in the presence of catalase."

p(MGI:Prdx2) neg bp(MESH:"Oxidative Stress")

SET Evidence = "The
appreciable increase in heme degradation for RBCs from PRDX2 knockout mice in the
presence of azide indicates that PRDX2 plays a major role in scavenging H2O2 in the
absence of catalase, even with GPx present."

p(MGI:Prdx2) neg a(CHEBI:"hydrogen peroxide")

SET TextLocation = "Discussion"

SET Evidence = "GPx
removes both H2O2 and organic hydroperoxides [8,31] whereas PRDX2 removes H2O2 [2],
organic hydroperoxides, lipid hydroperoxides, [32,33] peroxynitrite [34] and protein
hydroperoxides [35]."

p(PFAM:GSHPx) neg a(CHEBI:"hydrogen peroxide")
p(PFAM:GSHPx) neg a(CHEBI:hydroperoxide)

p(MGI:Prdx2) neg a(CHEBI:"hydrogen peroxide")
p(MGI:Prdx2) neg a(CHEBI:hydroperoxide)
p(MGI:Prdx2) neg a(CHEBI:"lipid hydroperoxide")
p(MGI:Prdx2) neg a(CHEBI:peroxynitrite)

SET Evidence = "The finding that GPx has a greater role in the inhibition of heme degradation products than
catalase [3] is consistent with the primary role of catalase to react with the high
concentrations of H2O2 coming from exogenous sources and for GPx to react with the low
levels of H2O2 coming from the endogenous autoxidation of Hb [39]."

p(PFAM:GSHPx) neg a(CHEBI:"hydrogen peroxide")

SET Evidence = "The in vivo effects that we have observed for PRDX2 knockout mice (Figures 1â€“4) imply
that PRDX2 plays an important role in neutralizing the H2O2 generated in vivo (Figure 6)."

p(MGI:Prdx2) neg a(CHEBI:"hydrogen peroxide")

SET Evidence = "PRDX2, which is able to react with low levels of H2O2 even at reduced
glutathione levels, may therefore play a role in limiting the increased formation of heme
degradation products in older cells."

p(MGI:Prdx2) neg bp(MESH:"Oxidative Stress")

SET Evidence = "The role of PRDX2 in inhibiting impaired deformability can be attributed to both a reduction
in ROS as well as a direct reaction of PRDX2 with protein hydroperoxides [52], which will
inhibit the damage to cytoskeletal proteins required for impaired deformability."

p(MGI:Prdx2) pos bp(MESH:"Erythrocyte Deformability")
a(MESH:"Reactive Oxygen Species") neg bp(MESH:"Erythrocyte Deformability")
p(MGI:Prdx2) neg a(MESH:"Reactive Oxygen Species")
a(CHEBI:hydroperoxide) neg bp(MESH:"Erythrocyte Deformability")
p(MGI:Prdx2) neg a(CHEBI:hydroperoxide)

UNSET TextLocation
##############