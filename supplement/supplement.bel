#######################
# Document Properties #
#######################

SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "farah.humayun@scai-extern.fraunhofer.de"

SET DOCUMENT Name        = "HemeKG Supplement"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Farah Humayun"
SET DOCUMENT Description = "Heme BEL model is a disease model based on Biological Expression Language (BEL) containing the core mechanisms of heme as modulator in Hemolytic diseases such as sickle-cell anemia, beta-thalassemia, malaria and autoimmune hemolytic anemia."

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

DEFINE NAMESPACE HM        AS PATTERN ".*"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------

DEFINE ANNOTATION Species       AS PATTERN ".*"
DEFINE ANNOTATION TimePoint     AS PATTERN ".*"
DEFINE ANNOTATION Concentration     AS PATTERN ".*"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "PLoS One. 2015 Nov 10;10(11):e0142328. doi: 10.1371/journal.pone.0142328. eCollection 2015.", "26555697"}

SET Evidence = "We have previously shown that free heme is a potent apoptotic factor as well as inducer of
the pro-inflammatory chemokine CXCL10 in microvasculature".

a(CHEBI:heme) pos p(HGNC:CXCL10)

SET Evidence = "In both human and murine malaria infection, increases in
heme, CXCL10 and TLR4 and TLR9 have been shown to regulate the host immune response to
Plasmodium infection [47–49]."

a(CHEBI:heme) -> act(p(HGNC:TLR4))
p(HGNC:TLR4) -> p(HGNC:CXCL10)

SET Evidence = "In the present study, we have shown that heme-induced expression
of CXCL10 and apoptosis was mediated by TLR4 in HBVEC and CD34+-HSPC."

a(CHEBI:heme) -> act(p(HGNC:TLR4))
p(HGNC:TLR4) -> p(HGNC:CXCL10)

SET Evidence = "Therefore, we propose a pathophysiological
mechanism whereby heme mediates the TLR4 signaling pathway, resulting in overproduction
of cytotoxic CXCL10."

a(CHEBI:heme) -> act(p(HGNC:TLR4))
p(HGNC:TLR4) -> p(HGNC:CXCL10)

SET Evidence = "HBVEC showed significant reductions in expression of CXCL10 in the presence of the
TLR4-blocking agent anti-CD14 in vitro conditions, though decreases in CXCL10 expression
in CD34+-HSPC did not reach statistical significance."

p(HGNC:TLR4) -> p(HGNC:CXCL10)

UNSET Citation
###########

SET Citation = {"PubMed", "Innate Immun. 2011 Feb;17(3):293-301. doi: 10.1177/1753425910369020. Epub 2010 May 14.", "20472612"}

SET Evidence = "High concentrations
of hemin (50 mM) triggered TLR4-mediated IL-8 
production in the human HEK293/TLR4 cell line 
in the absenceof the co-receptors CD14 and MD-2; 
the latter an essential co-receptor for TLR4 
activation by endotoxin."

a(CHEBI:heme) -> act(p(HGNC:TLR4))
p(HGNC:TLR4) -> p(HGNC:CXCL8)

SET Evidence = "We
show that hemin can induce TLR4-dependent cell
activation in a human cell line in both a
MD-2/CD14-dependent and –independent manner."

p(HGNC:CD14) pos complex(p(HGNC:TLR4), a(CHEBI:heme))

SET Evidence = "The studies reported here confirm and extend the recent
observations by Figueiredo et al.20 that showed TLR4-
and CD14-dependent cell activation by hemin using
mouse macrophages."

SET Cell = "macrophage"
p(MGI:Cd14) pos complex(p(MGI:Tlr4), a(CHEBI:heme))

SET Evidence = "We have demonstrated
TLR4-dependent activation of a human HEK293/TLR4
cell line by hemin that was MD-2/CD14-dependent at
hemin concentrations 10 mM, but was MD-2/
CD14-independent at higher hemin concentrations
(50 mM; Fig. 2A)."

#higher concentrations of heme
p(HGNC:CD14) pos complex(p(HGNC:TLR4), a(CHEBI:heme))

SET Evidence = "The ability of heme to act
as a ‘danger’ signal may be linked to its ability to initiate
TLR4-dependent as well as TLR4-independent responses."

a(CHEBI:heme) -> act(p(HGNC:TLR4))

UNSET Citation
###########


SET Citation = {"PubMed", "J Biol Chem. 2007 Jul 13;282(28):20221-9. Epub 2007 May 14.", "17502383"}

SET Evidence = "Degradation of IkBa
by heme and LPS indicated the
activation of the NFkB signaling
pathway (Fig. 5A)."

a(CHEBI:heme) -> deg(p(HGNC:NFKBIA))
deg(p(HGNC:NFKBIA)) -> act(p(HGNC:NFKB1))

SET Evidence = "The MyD88 pathway leads to
the activation of NFkB and MAPKs and the induction of cytokines
such as TNF-alpha, KC, IL-6, and IL-12."

p(HGNC:MYD88) -> act(p(HGNC:NFKB1))
act(p(HGNC:NFKB1)) -> p(HGNC:TNF)
act(p(HGNC:NFKB1)) -> p(HGNC:IL6)
act(p(HGNC:NFKB1)) -> complex(p(HGNC:IL12A), p(HGNC:IL12B))
act(p(HGNC:NFKB1)) -> a(MESH:"keratinocyte-derived chemokines")

SET Evidence = "Similarly, heme induced the expression of TNF-a
mRNA but not of IL-6 or IFN-b by macrophages (data not
shown)."

a(CHEBI:heme) -> p(HGNC:TNF)

SET Evidence = "Degradation of IkBa
by heme and LPS indicated the
activation of the NFkB signaling
pathway (Fig. 5A). Activation of
macrophages and DC by LPS causes
TLR4-dependent phosphorylation
of selected tyrosine kinases such as
p38 and ERK1/2 (2, 4). The stimulation of macrophages by
heme caused the phosphorylation of ERK1/2 but only modestly
affected p38 (Fig. 5, B and C)."

a(CHEBI:heme) -> deg(p(HGNC:NFKBIA))
deg(p(HGNC:NFKBIA)) -> act(p(HGNC:NFKB1))
SET Cell = "macrophage"
a(CHEBI:heme) -> p(HGNC:MAPK3, pmod(Ph)) 
a(CHEBI:heme) -> p(HGNC:MAPK1, pmod(Ph))
a(CHEBI:heme) -> p(HGNC:MAPK14, pmod(Ph))

SET Evidence = "Heme caused
degradation and ERK phosphorylation
similar to LPS, whereas p38 phosphorylation 
was modestly induced by heme."

a(CHEBI:heme) -> deg(p(HGNC:NFKBIA))
a(CHEBI:heme) -> p(HGNC:MAPK3, pmod(Ph)) 
a(CHEBI:heme) -> p(HGNC:MAPK1, pmod(Ph))

UNSET Citation
###########

SET Citation = {"PubMed", "J Pineal Res. 2012 Aug;53(1):67-76. doi: 10.1111/j.1600-079X.2012.00972.x. Epub 2012 Jan 31.", "22288937"}

SET Evidence = "TLR4 signaling is mediated by MyD88-dependent and
MyD88-independent pathways and ultimately induces the
expression of inflammatory gene products such as cytokines
and chemokines [34]. While MyD88 is a common downstream
adaptor molecule that induces activation of mitogen-
activated protein kinases and NF-kB transcription
factor, the MyD88-independent pathway, that is, TRIFdependent
pathway, is responsible for the activation of
interferon regulatory factor 3 (IRF3) and subsequent
induction of IFN-b and IFN-inducible gene products
(e.g., CXCL-10)."

p(HGNC:TLR4) -> act(p(HGNC:MYD88))
p(HGNC:TLR4) -> act(p(HGNC:TICAM1))
p(HGNC:MYD88) -> act(p(HGNC:NFKB1))
p(HGNC:TICAM1) -> act(p(HGNC:IRF3))
p(HGNC:IRF3) -> p(HGNC:IFNB1)
p(HGNC:IRF3) -> p(HGNC:CXCL10)

SET Evidence = "IFN-b elicits the secondary response to TLR4 activation
and contributes to further upregulation of CXCL-10, an
instrumental chemokine in the neutrophil-mediated phase
of hepatic inflammation and I/R injury [37]."

p(HGNC:IFNB1) pos p(HGNC:CXCL10)

UNSET Citation
###########

SET Citation = {"PubMed", "Am J Physiol Renal Physiol. 2018 May 1;314(5):F906-F914. doi: 10.1152/ajprenal.00432.2017. Epub 2017 Oct 4.", "28978536"}

#SET Species = "Mice"

SET Evidence = "We employed MCP-1 expression as a 
readout for renal inflammation (19, 45).
As shown in Figures 2 and 3 respectively, heme markedly increased expression of MCP-1
mRNA and MCP-1 protein content in the supernatant of heme-exposed cells, and both these
effects were significantly reduced by TAK-242."

a(CHEBI:heme) pos p(MGI:Ccl2)

UNSET Citation
###########

